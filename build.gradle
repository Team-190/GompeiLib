plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.diffplug.spotless" version "6.25.0"
    id "io.freefair.lombok" version "8.4"
    id "io.freefair.aspectj.post-compile-weaving" version "8.4"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


group = 'edu.wpi.team190'
version = project.hasProperty("releaseVersion") ? project.property("releaseVersion") : "0.0.0-SNAPSHOT"

repositories {
    mavenCentral()
    maven { url = uri("https://frcmaven.wpi.edu/artifactory/release") }
    maven { url = uri("https://frcmaven.wpi.edu/artifactory/littletonrobotics-mvn-release") }
    maven { url = uri("https://maven.ctr-electronics.com/release/") }
    maven { url = uri("https://frcmaven.wpi.edu/artifactory/sleipnirgroup-mvn-release/") }
}

dependencies {
    // WPILib bundle
    compileOnly libs.bundles.wpilib

    // AspectJ
    compileOnly libs.aspectj.rt
    runtimeOnly libs.aspectj.tools

    // CTRE
    compileOnly libs.phoenix6

    // Choreo
    compileOnly libs.choreo

    // AdvantageKit + runtime dependencies
    compileOnly(libs.bundles.advantagekit)
    annotationProcessor libs.advantagekit.autolog

    // Tests
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = group
            artifactId = "gompeilib" // or whatever you want the artifact name to be
            version = version
        }
    }

    repositories {
        // GitHub Packages ONLY in CI
        if (System.getenv("CI") == "true") {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/Team-190/GompeiLib")
                credentials {
                    username = System.getenv("GPR_USER") ?: project.findProperty("gpr.user")
                    password = System.getenv("GPR_KEY") ?: project.findProperty("gpr.key")
                }
            }
        }
        // NOTE: DO NOT add mavenLocal() here
        // Gradle already provides publishToMavenLocal automatically
    }
}



test {
    useJUnitPlatform()
}

// Spotless formatting
project.compileJava.dependsOn(spotlessApply)
spotless {
    java {
        target fileTree(".") {
            include "**/*.java"
            exclude "**/build/**", "**/build-*/**"
        }
        toggleOffOn()
        googleJavaFormat("1.19.2")
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    json {
        target fileTree(".") {
            include "**/*.json"
            exclude "**/build/**", "**/build-*/**"
        }
        gson().indentWithSpaces(2)
    }
    format "misc", {
        target fileTree(".") {
            include "**/*.md", "**/.gitignore"
            exclude "**/build/**", "**/build-*/**"
        }
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
}

tasks.named("build") {
    dependsOn(gradle.includedBuild("swerve").task(":build"))
}
